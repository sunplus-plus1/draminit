#include "include/config.h"

OUTPUT_FORMAT("elf32-littlearm", "elf32-littlearm", "elf32-littlearm")
OUTPUT_ARCH(arm)
ENTRY(_start)

#define BIN_ADDR     0x9e805040      /* 0x40: 64-byte header */
#define BIN_SIZE     (7K - 64)

MEMORY
{
	public : ORIGIN = BIN_ADDR           , LENGTH = BIN_SIZE   /* draminit bin */
	sram   : ORIGIN = BIN_ADDR + BIN_SIZE, LENGTH = 1K + 512   /* borrow sect_buf */
}

SECTIONS
{
	.text :
	{
		start.o (.text)
		*(.rodata)
		*(.text)
		*(.text.*)
	} > public

	.data :
	{
		*(.data*)
	} > sram

	.bss :
	{
		__bss_start = .;
		*(.bss)
		*(.bss.*)
		*(.sbss*)
		*(COMMON)
		. = ALIGN(4);
		__bss_end__ = .;
	} > sram
}
